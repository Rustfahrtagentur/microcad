
/// Circle definition
///
/// Examples:
/// * with radius `r`: `circle(r = 10.0mm);`
/// * with diameter `d`: `circle(d = 5.0mm);`
part circle(radius: Length, center = (x = 0.0mm, y = 0.0mm)) {
    init(r: Length, c = (x = 0.0mm, y = 0.0mm)) {
        radius = r;
        center = c;
    }

    init(diameter: Length, center = (x = 0.0mm, y = 0.0mm)) {
        radius = diameter / 2.0;
    }

    init(d: Length, center = (x = 0.0mm, y = 0.0mm)) {
        radius = d / 2.0;
    }

    __builtin::geo2d::circle(radius = radius / 1mm, cx = center.x / 1mm, cy = center.y / 1mm);
}

/// Rectangle definition
///
/// Examples:
/// * centered rect with width `w` and height `h`: `rect(width = 10.0mm, height = 5.0mm);`
part rect(width: Length, height: Length, x: Length, y: Length) {
    init(width: Length, height: Length) {
        x = -width / 2.0; // center x
        y = -height / 2.0; // center y
    }

    init(size: Length) {
        width = size;
        height = size;
        x = -size / 2.0; // center x
        y = -size / 2.0; // center y
    }

    __builtin::geo2d::rect(
            width / 1mm,
            height / 1mm,
            x / 1mm,
            y / 1mm
    );
}
