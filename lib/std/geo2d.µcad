
/// Circle definition
///
/// Examples:
/// * with radius `r`: `Circle(r = 10.0mm);`
/// * with diameter `d`: `Circle(d = 5.0mm);`
sketch Circle(radius: Length, center = (x = 0.0mm, y = 0.0mm)) {
    init(r: Length, c = (x = 0.0mm, y = 0.0mm)) {
        radius = r;
        center = c;
    }

    init(diameter: Length, center = (x = 0.0mm, y = 0.0mm)) {
        radius = diameter / 2.0;
    }

    init(d: Length, c = (x = 0.0mm, y = 0.0mm)) {
        radius = d / 2.0;
        center = c;
    }

    __builtin::geo2d::Circle(radius = radius / 1mm, cx = center.x / 1mm, cy = center.y / 1mm);
}

/// Rectangle definition
///
/// Examples:
/// * centered rect with width `w` and height `h`: `rect(width = 10.0mm, height = 5.0mm);`
sketch Rect(width: Length, height: Length, x: Length, y: Length) {
    init(width: Length, height: Length) {
        x = -width / 2.0; // center x
        y = -height / 2.0; // center y
    }

    init(w: Length, h: Length) {
        width = w;
        height = h;
        x = -w / 2.0; // center x
        y = -h / 2.0; // center y
    }

    init(size: Length) {
        width = size;
        height = size;
        x = -size / 2.0; // center x
        y = -size / 2.0; // center y
    }

    __builtin::geo2d::Rect(
            width = width / 1mm,
            height = height / 1mm,
            x = x / 1mm,
            y = y / 1mm
    );
}


/// A frame with a thickness.
sketch Frame(width: Length, height: Length, thickness: Length) {
    std::debug::assert(thickness > 0.0mm, "Thickness must be a positive number (thickness = {thickness})");
    Rect(width, height) - Rect(width = width - 2*thickness, height = height - 2*thickness)
}

sketch Line(p0: Vec2, p1: Vec2) {
    init(width: Length, y = 0.0mm) {
        p0 = (x = -width / 2mm, y = y / 1mm);
        p1 = (x =  width / 2mm , y = y / 1mm);
    }

    init(height: Length, x = 0.0mm) {
        p0 = (x = x / 1mm, y = -height / 2mm);
        p1 = (x = x / 1mm, y =  height / 2mm);
    }

    init(width: Length, p: (x: Length, y: Length)) {
        p0 = (x = p.x / 1mm, y = p.y / 1mm);
        p1 = (x = (p.x + width) / 1mm , y = p.y / 1mm);
    }

    __builtin::geo2d::Line(x0 = p0.x, y0 = p0.y, x1 = p1.x, y1 = p1.y);
}

sketch Ring(outer_r: Length, inner_r: Length ) {
     init(outer_r: Length, thickness: Length) {
         inner_r = outer_r - thickness;
     }

     init(outer_d: Length, inner_d: Length ) {
         outer_r = outer_d/2;
         inner_r = inner_d/2;
     }
     init(outer_d: Length, thickness: Length) {
         inner_r = outer_d/2 - thickness;
         outer_r = outer_d/2;
     }

     Circle(radius = outer_r) - Circle(radius = inner_r) 
}