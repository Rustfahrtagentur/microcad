pub use __builtin::ops::subtract;
pub use __builtin::ops::intersect;
pub use __builtin::ops::union;
pub use __builtin::ops::hull;
pub use __builtin::ops::align;


pub op revolve(angle = 360°) {
    @input.__builtin::ops::revolve(
        circular_segments = 64, // TODO: Make this adaptive to resolution 
        revolve_degrees = angle / 1°
    );
}

pub op extrude(height: Length) {
	init(z: Length) {
		height = z;
	}

    @input.__builtin::ops::extrude(height = height / 1mm, n_divisions = 0, twist_degrees = 0.0, scale_top_x = 1.0, scale_top_y = 1.0);
}

pub op translate(x = 0.0mm, y = 0.0mm, z = 0.0mm) {
	@input.__builtin::ops::translate(x = x / 1mm, y = y / 1mm, z = z / 1mm);
}

/// An operation that rotates a part.
///
/// See the `init`s for examples on how to use this operation. 
pub op rotate(matrix: Matrix3) {
	/// Rotate around an axis by an angle using a 3D vector.
	/// 
	/// The `axis` vector will be normalized. 
	///
	/// * `angle` - An angle.
	/// * `axis` - An axis to rotate around (Z axis is default).
	///
	/// Examples:
	/// * `Rect(42mm).rotate(45°);`: 2D rotation of a square by 45°.
	/// * `Cube(42mm).rotate(45°, (x = 1, y = 1, z = 1)) ;`: Rotates a cube by 45° around a diagonal axis.
	/// * `Cube(42mm).rotate(30°, Y);`: Rotates a cube by 30° around Y axis.
	init(angle: Angle, axis = __builtin::math::Z) {
		matrix = __builtin::math::rotate_around_axis(angle, x = axis.x, y = axis.y, z = axis.z);
	}
	
	/// Euler rotation around X, Y, Z.
	///
	/// * `x` - X angle.
	/// * `y` - Y angle.
	/// * `z` - Z angle.
	///
	/// Examples:
	/// * `Cube(42mm).rotate(45°, (1, 0, 0));`: Rotates a cylinder by 45° around X axis (unnamed tuple).
	init(x = 0°, y = 0°, z = 0°) {
		matrix = __builtin::math::rotate_xyz(x, y, z);		
	}
	
	/// Euler rotation around X, Y, Z (nautical angles)
	///
	/// * `roll` - Roll angle, rotates around X axis.
	/// * `pitch` - Pitch angle, rotates around Y axis.
	/// * `yaw` - Yaw angle, rotates around Z axis.
	///
	/// Example:
	/// * `Cube(42mm).rotate(roll = 30°)`: Rotates a cylinder by 45° around X axis.
	init(roll = 0°, pitch = 0°, yaw = 0°) {
		matrix = __builtin::math::rotate_xyz(roll, pitch, yaw);
	}
	
	/// Euler rotation around X, Y and Z axis (with a vector of angles)
	///
	/// * `Cube(42mm).rotate(xyz = (30°,20°,10°))`: Euler rotation for a cube around X, Y, Z axis.
	init(xyz = (x = 0°,y = 0°,z = 0°)) {
		matrix = __builtin::math::rotate_xyz(xyz.x, xyz.y, xyz.z);
	}
	
	/// Euler rotation around Z, Y and X axis (with a vector of angles)
	///
	/// * `Cube(42mm).rotate(zyx = (30°,20°,10°));`: Euler rotation for a cube around Z, Y, X axis.
	init(zyx = (x = 0°, y = 0°, z = 0°)) {
		matrix = __builtin::math::rotate_zyx(zyx.z, zyx.y, zyx.x);
	}

	@input.__builtin::ops::rotate(matrix);
}

/// An operation that scales a part.
///
/// See the `init`s for examples on how to use this operation. 
op scale(v: Vec3) {
	/// Uniform scale.
	/// 
	/// * `s` - Scale factor.
	///
	/// Examples:
	/// * `Rect(42mm).scale(200%);`: Scale a rectangle by 200%.
	init(s: Scalar) {
		v = (x = s, y = s, z = s);
	}

	/// Non-uniform scale.
	/// 
	/// * `x` - Scale factor in X direction.
	/// * `y` - Scale factor in Y direction.
	/// * `z` - Scale factor in Z direction.
	///
	/// Examples:
	/// * `Rect(42mm).scale(x = 2.0);`: Scale a rectangle in X direction.
	init(x: Scalar = 1.0, y: Scalar = 1.0, z: Scalar = 1.0) {
		v = (x = x, y = y, z = z);
	}

	@input.__builtin::ops::scale(x = v.x, y = v.y, z = v.z);
}


/// Orient an object towards a vector.
pub op orient(v: Vec3) {
    @input.__builtin::ops::orient(x = v.x, y = v.y, z = v.z);
}
