use std::*;
use std::geo2d::*;
use std::ops::*;

const SPACING = 8mm;

op grid(rows: Integer, columns: Integer) {
    @input
        .translate(x = [1..rows] * SPACING, y = [1..columns] * SPACING)
        .align()
}

part LegoBrick(rows: Integer, columns: Integer, a = 8mm, base_height = 9.6mm) {
    wall = 1.2mm;
    width = rows * a - 0.2mm;
    height = columns * a - 0.2mm;
    top_thickness = 1.0mm;

    base = {
        frame = Rect(width, height) - Rect(width = width - wall * 2, height = height - wall * 2);
        struts = (Circle(d = 6.51mm) - Circle(d = 4.8mm))
            .grid(rows = rows - 1, columns = columns - 1);
        frame | struts;
    }
    .extrude(height = base_height - top_thickness);

    top = Rect(width, height)
        .extrude(height = top_thickness)
        .translate(z = base_height - top_thickness);
    
    knobs = Circle(d = 4.8mm)
        .grid(rows, columns)
        .extrude(height = 1.7mm)
        .translate(z = base_height);

    base | top | knobs;
}

LegoBrick(rows = 2, columns = 2, base_height = 9.6mm * 2)
    .translate(y = -40mm);

LegoBrick(rows = 4, columns = 2);

LegoBrick(rows = 3, columns = 2, base_height = 3.2mm)
    .translate(y = 40mm);
