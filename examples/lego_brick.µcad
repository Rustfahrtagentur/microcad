use std::*;
use std::geo2d::*;
use std::ops::extrude;
use std::ops::translate;

part LegoBrick(rows: Integer, columns: Integer, a = 8mm, base_height = 9.6mm) {
    wall = 1.2mm;
    width = rows * a - 0.2mm;
    height = columns * a - 0.2mm;
    top_thickness = 1.0mm;

    base = {
        frame = Rect(width, height) - Rect(width = width - wall * 2, height = height - wall * 2);
        struts = (Circle(d = 6.51mm) - Circle(d = 4.8mm))
            .translate(x = [0..rows-1] * a - a * (rows-2) / 2, y = [0..columns-1] * a - a * (columns-2) / 2);
        frame | struts;
    }
    .extrude(height = base_height - top_thickness);

    top = Rect(width, height)
        .extrude(height = top_thickness)
        .translate(z = base_height - top_thickness);
    
    knobs = Circle(d = 4.8mm, c = (x = [0..rows] * a, y = [0..columns] * a))
        .extrude(height = 1.7mm)
        .translate(x = -(width - a) / 2, y = -(height - a) / 2, z = base_height);

    base | top | knobs;
}

LegoBrick(rows = 2, columns = 2, base_height = 9.6mm * 2)
    .translate(y = -40mm);

LegoBrick(rows = 4, columns = 2);

LegoBrick(rows = 3, columns = 2, base_height = 3.2mm)
    .translate(y = 40mm);
