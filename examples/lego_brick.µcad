use std::*;
use std::geo2d::*;
use std::ops::extrude;
use std::ops::translate;

part lego_brick(rows: Integer, columns: Integer, a = 8mm, base_height = 9.6mm) {
    wall = 1.2mm;
    width = rows * a - 0.2mm;
    height = columns * a - 0.2mm;
    top_thickness = 1.0mm;

    base = extrude(height = base_height - top_thickness) {
        translate(x = [0..rows-1] * a - a / 2 * (rows-2), y = [0..columns-1] * a - a / 2 * (columns-2)) {
            circle(d = 6.51mm) - circle(d = 4.8mm);
        }
        rect(width, height) - rect(width = width - wall * 2, height = height - wall * 2);
    };

    top = translate(z = base_height - top_thickness)
        extrude(height = top_thickness)
        rect(width, height);
    
    knobs = translate(z = base_height)
        extrude(height = 1.7mm) {
            translate(x = [0..rows] * a - (width - a) / 2, y = [0..columns] * a - (height - a) / 2)
                circle(d = 4.8mm);
        };

    base | top | knobs;
}

translate(y = -40mm) lego_brick(rows = 2, columns = 2, base_height = 9.6mm * 2);

lego_brick(rows = 4, columns = 2);

translate(y = 40mm) lego_brick(rows = 3, columns = 2, base_height = 3.2mm);
